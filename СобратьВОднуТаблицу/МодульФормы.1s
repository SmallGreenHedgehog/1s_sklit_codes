Процедура Sleep(Задержка) 
   Shell=СоздатьОбъект("WScript.Shell");	
   Shell.Run("ping -n 1 127.255.255.255 -w "+СокрЛП(Задержка*1000),0,-1);
   Shell=ПолучитьПустоеЗначение(); 
КонецПроцедуры

Процедура ВывестиТаблицу(Табл, МаксСтрок=0, Отступ=0, Разделит="		")
	Перем ВывестиСтрок; 
	Перем СтрКол, К, ТипТабл, ТекТипТабл;
	
	ТекТипТабл=ТипЗначенияСтр(Табл);
	ТипТабл=?(ТекТипТабл="ТаблицаЗначений",1,?(ТекТипТабл="ИндексированнаяТаблица",2,0));
	
	Если ТипТабл>0 Тогда 
		СтрКол="";  
		Для К=1 По Отступ Цикл
			СтрКол=СтрКол+Разделит;
		КонецЦикла;
		Для К=1 По Табл.КоличествоКолонок() Цикл
			СтрКол=СтрКол+СокрЛП(?(ТипТабл=1,Табл.ПолучитьПараметрыКолонки(К),Табл.ИмяКолонки(К)))+"; ";
		КонецЦикла;
		Сообщить(СтрКол);
		
		ВывестиСтрок=?(МаксСтрок>0,Мин(МаксСтрок,Табл.КоличествоСтрок()),Табл.КоличествоСтрок());
		Для Стр=1 по ВывестиСтрок Цикл
			СтрТекст="";
			Для К=1 По Отступ Цикл
				СтрТекст=СтрТекст+Разделит;
			КонецЦикла;
			Для Кол=1 По Табл.КоличествоКолонок() Цикл
				СтрТекст=СтрТекст+СокрЛП(Табл.ПолучитьЗначение(Стр,Кол))+";";			 
			КонецЦикла;
			СтрТекст=Лев(СтрТекст,СтрДлина(СтрТекст)-1);
			Сообщить(СтрТекст); 	
			
			Если ТипТабл=2 Тогда
				Если (Табл.ИмяКолонки(Кол-1)="тзПотомки") Тогда
					ВывестиТаблицу(Табл.ПолучитьЗначение(Стр,Кол-1),,Отступ+1);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;  
	Иначе
		Сообщить("Вывод таблицы не возможен. Переданные данные не являются таблицей. ("+ТекТипТабл+")");
	КонецЕсли;
КонецПроцедуры

Процедура ВыборКатКодов()
	Перем ИмяКат;
	
	Если ФС.ВыбратьКаталог(ИмяКат, "Выбирете каталог с файлами кодов.")=1 Тогда
		КатКодов=ИмяКат;
	КонецЕсли;
КонецПроцедуры // ВыборКатКодов

Функция НайтиФайлыВКаталоге(Расшир, Дир, Файлы)
	Перем СписФайловВКаталоге, НеНайденыФайлы, ПорядНомПопытПоиска, ТекФайл; 
	ПорядНомПопытПоиска=0;
	СписФайловВКаталоге=СоздатьОбъект("СписокЗначений");
	//Если файлы найдены заполняем список   
	ТекФайл="";
	НеНайденыФайлы=0;
	Пока (НеНайденыФайлы=0) Цикл 
		ПорядНомПопытПоиска=ПорядНомПопытПоиска+1;
		Если ПорядНомПопытПоиска>1 Тогда
			ТекФайл=ФС.НайтиСледующийФайл();		
		Иначе 
			ТекФайл=ФС.НайтиПервыйФайл(Дир+"\*."+Расшир);
		КонецЕсли;
		
		Если ПустаяСтрока(ТекФайл)=1 Тогда  
			НеНайденыФайлы=1;
		Иначе 
			СписФайловВКаталоге.ДобавитьЗначение(ТекФайл);			
		КонецЕсли;
	КонецЦикла; 
	Файлы=СписФайловВКаталоге;
	
	Возврат СписФайловВКаталоге.РазмерСписка();
КонецФункции   

Процедура СоздатьЕдинуюТаблицу()
	Перем СписИменФайлов, КолФайлов;
	Перем База, Запрос,  ТекстДБФЗапроса, ТабДБФЗапроса;	
	Перем К, ТекТаб, ТекФайл;
	
	//СписИменФайлов=СоздатьОбъект("СписокЗначений");
	
	КолФайлов=НайтиФайлыВКаталоге("dbf",КатКодов,СписИменФайлов);
	
	Если КолФайлов>0 Тогда		
		//Запрос получения записей во всех файлах				
		ТекстДБФЗапроса="
		|INSERT INTO BIG_CODS (Id_Cl, Id_GRP, KOD, DELETED)
		|SELECT
		|	Tables.ID_CL,
		|	Tables.ID_GRP,
		|	Tables.KOD,
		|	'n'
		|FROM
		|	(";
		
		
		//Теперь добавляем все таблички
		Для К=1 По КолФайлов Цикл
			ТекФайл=СписИменФайлов.ПолучитьЗначение(К,);
			ТекТаб=Лев(ТекФайл,СтрДлина(ТекФайл)-4);
			
			
			ТекстДБФЗапроса=ТекстДБФЗапроса+"			
			|	SELECT
			|		"+ТекТаб+".ID_CL,
			|		"+ТекТаб+".ID_GRP,
			|		"+ТекТаб+".KOD
			|	FROM
			|		"+ТекТаб+"
			|	UNION ALL";
		КонецЦикла;
		
		ТекстДБФЗапроса=Лев(ТекстДБФЗапроса, СтрДлина(ТекстДБФЗапроса)-10);
		
		ТекстДБФЗапроса=ТекстДБФЗапроса+") as Tables
		//|WHERE
		//|	Tables.ID_GRP<9999
		|GROUP by 
		|	Tables.ID_CL,
		|	Tables.ID_GRP,
		|	Tables.KOD
		|";
		
		База = СоздатьОбъект("OLEDBData");
		Рез=База.Connect("Provider=VFPOLEDB.1;Data Source="+КатКодов+";Mode=ReadWrite;Collating Sequence=MACHINE");
		Если Рез=1 Тогда		
			Запрос = База.СоздатьКоманду();
			
			НеобходКолПопыт=5;
			ТекПопыт=0;
			Успех=0;        
			
			Пока (ТекПопыт<НеобходКолПопыт) и (Успех=0) Цикл
				ТекПопыт=ТекПопыт+1;
				Успех=1;
				Попытка
					Запрос.ВыполнитьИнструкцию("CREATE TABLE BIG_CODS (Id_Cl Integer (4,0), Id_GRP Integer (4,0), KOD Character (20,0), DELETED Logical (1,0))");
				Исключение
					Успех=0; 
					Сообщить(ОписаниеОшибки());
					Sleep(1);			
				КонецПопытки;
			КонецЦикла; 
			
			Если Успех=1 Тогда
				НеобходКолПопыт=5;
				ТекПопыт=0;
				Успех=0;        
				
				Пока (ТекПопыт<НеобходКолПопыт) и (Успех=0) Цикл
					ТекПопыт=ТекПопыт+1;
					Успех=1;
					Попытка
						Запрос.ВыполнитьИнструкцию(ТекстДБФЗапроса);
					Исключение
						Успех=0; 
						Сообщить(ОписаниеОшибки());
						Sleep(1);			
					КонецПопытки;
				КонецЦикла; 
				
				Если Успех=1 Тогда
					//ВывестиТаблицу(ТабДБФЗапроса);
					Сообщить("Создана таблица ""BIG_CODS"".");
				КонецЕсли;
			КонецЕсли;
			Запрос=ПолучитьПустоеЗначение();
		КонецЕсли;
	Иначе
		Сообщить("Не найдено ни одного файла в каталоге");
	КонецЕсли;
КонецПроцедуры // СоздатьЕдинуюТаблицу

Процедура СобратьВОднуТаб()
	Если ПустаяСтрока(СокрЛП(КатКодов))=0 Тогда
		Если ФС.СуществуетФайл(КатКодов)=1 Тогда
			СоздатьЕдинуюТаблицу();
		Иначе
			Предупреждение("Данный каталог не найден!");
		КонецЕсли;
	Иначе
		Предупреждение("Сначала необходимо выбрать каталог с кодами.");
	КонецЕсли;	
КонецПроцедуры // СобратьВОднуТаб